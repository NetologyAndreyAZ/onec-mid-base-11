#Область ПрограммныйИнтерфейс

Процедура ДополнитьФорму(Форма) Экспорт
	
	ИмяФормы = Форма.ИмяФормы;
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппеШапкаПраво(Форма);
		ДобавитьГруппуСкидкаИПересчетаВГруппеШапкаЛево(Форма);
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента"
			ИЛИ ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппеШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента"
			ИЛИ ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти 

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьГруппуСкидкаИПересчетаВГруппеШапкаЛево(Форма)
	
	ГруппаФормы = Форма.Элементы.Добавить("ГруппаСкидкаИПересчета", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	ГруппаФормы.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаФормы.Заголовок = "Группа скидка и пересчета";
	ГруппаФормы.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	ГруппаФормы.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаФормы.ОтображатьЗаголовок = ЛОЖЬ;
	ГруппаФормы.РастягиватьПоГоризонтали = ИСТИНА;
	
	
	ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаСкидкаИПересчета);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.Заголовок = "Согласованная скидка";
	ПолеВвода.ПутьКДанным = "Объект.ДЗ_СогласованнаяСкидка";
	ПолеВвода.УстановитьДействие("ПриИзменении", "СогласованнаяСкидкаПриИзмененииВместо");

    КнопкаФормы = Форма.Элементы.Добавить("ДЗ_ПересчетТаблиц", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСкидкаИПересчета);
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	КнопкаФормы.ИмяКоманды = "ДЗ_ПересчетТаблиц";
	КнопкаФормы.Заголовок = "Пересчетать таблицу";
	
КонецПроцедуры

Процедура ДобавитьПолеКонтактноеЛицоВГруппеШапкаПраво(Форма)
	
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.Заголовок = "Контактное Лицо";
	ПолеВвода.ПутьКДанным = "Объект.ДЗ_КонтактноеЛицо";
	
КонецПроцедуры

Процедура ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма)
	
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.Заголовок = "Контактное Лицо";
	ПолеВвода.ПутьКДанным = "Объект.ДЗ_КонтактноеЛицо";
	
КонецПроцедуры

#КонецОбласти